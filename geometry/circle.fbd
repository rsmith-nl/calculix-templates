# file: circle.fbd
# Circle in the XY-plane, extruded in Z.
# vim:fileencoding=utf-8:fdm=marker:ft=cgx
#
# Author: R.F. Smith <rsmith@xs4all.nl>
# Created: 2023-09-08T16:34:19+0200
# Last modified: 2023-10-01T13:41:32+0200

# When using this code, replace “sfx” with a unique suffix.

## Define the view
rot y
rot r 135
rot u 30
frame
zoom .95

## Dimensions
# Radius
valu R-sfx / 50 1000
valu nR-sfx - 0 R-sfx
# Starting height
valu zs-sfx / 0 1000
# Thickness
valu t-sfx / 6 1000
# divisions
valu dl-sfx 24

## Geometry
pnt p-sfx1 0 0 zs-sfx
pnt p-sfx2 R-sfx 0 zs-sfx
pnt p-sfx3 0 R-sfx zs-sfx
pnt p-sfx4 nR-sfx 0 zs-sfx
pnt p-sfx5 0 nR-sfx zs-sfx
line l-sfx1 p-sfx1 p-sfx2 dl-sfx
line l-sfx2 p-sfx1 p-sfx3 dl-sfx
line l-sfx3 p-sfx2 p-sfx3 p-sfx1 dl-sfx
line l-sfx4 p-sfx1 p-sfx4 dl-sfx
line l-sfx5 p-sfx3 p-sfx4 p-sfx1 dl-sfx
line l-sfx6 p-sfx1 p-sfx5 dl-sfx
line l-sfx7 p-sfx4 p-sfx5 p-sfx1 dl-sfx
line l-sfx8 p-sfx5 p-sfx2 p-sfx1 dl-sfx
surf s-sfx1 blend l-sfx1 l-sfx2 l-sfx3
surf s-sfx2 blend l-sfx2 l-sfx4 l-sfx5
surf s-sfx3 blend l-sfx4 l-sfx6 l-sfx7
surf s-sfx4 blend l-sfx6 l-sfx1 l-sfx8
seta face-sfx1 s s-sfx1 s-sfx2 s-sfx3 s-sfx4
comp face-sfx1 do
seto cyl-sfx1
swep face-sfx1 face-sfx2 tra 0 0 t-sfx
setc

# Show the geometry
valu showgeom 0
if showgeom > 0
capt Circle face geometry.
plot pa face-sfx1
plus la face-sfx1
plus sa face-sfx1
stop
hcpy png circle-geometry
sys mogrify -trim circle-geometry.png
endif

## Create the mesh
valu showmesh 0
if showmesh > 0
capt Circle mesh.
elty all he20r
mesh all
view elem
plot e all n
view surf
stop
hcpy png circle-mesh
sys mogrify -trim circle-mesh.png
endif
