# file: sandwich.fbd
# vim:fileencoding=utf-8:fdm=marker:ft=cgx
#
# Copyright Â© 2021 R.F. Smith <rsmith@xs4all.nl>
# SPDX-License-Identifier: MIT
# Created: 2021-12-19T10:10:27+0100
# Last modified: 2023-10-15T19:51:37+0200

# Dimensions:
# length in [m]
valu L / 0.56 2
# Width in [m]
valu B 0.1
# Total thickness in [m]
valu T / 31 1000
# Top laminate thickness in [m]
valu t1 / 1.5 1000
# Bottom laminate thickness in [m]
valu t2 / 1.5 1000
# Core thickness in [m]
valu tmp + t1 t2
valu tk - T tmp

# Bottom laminate
seto lam2
    pnt ! 0 0 0
    swep lam2 new tra L 0 0
    swep lam2 new tra 0 B 0
    swep lam2 if1 tra 0 0 t2
setc

# Core
seto core
    copy if1 if2 tra 0 0 0
#    swep if2 if3 tra 0 0 tk tkdiv
    swep if2 if3 tra 0 0 tk
setc

# Top laminate
seto lam1
    copy if3 if4 tra 0 0 0
#    swep if4 top tra 0 0 t1 tbdiv
    swep if4 top tra 0 0 t1
setc

# Set element size automatically.
# This goes between geometry creation and meshing.
div all auto 0.005

## Create the meshes.
# he20 = C3D20
elty all he20
mesh lam2
mesh core
mesh lam1

# Constraints
seta nodes n all
enq nodes fix rec 0 _ _ 1e-4
# Remove nodes on dependant contact surfaces from constraints
enq nodes disp rec L _ _ 1e-4

# Show the mesh and selected nodes.
rot -z
rot d 30
rot r 30
frame
zoom 0.8
view elem
plus n fix r 8
plus n disp b 8

valu output no
# Write data
if output eq yes
neigh all 1e-5 tie
send all abq
send lam2 abq nam
send core abq nam
send lam1 abq nam
send fix abq nam
send disp abq nam
endif
